import java.text.SimpleDateFormat

plugins {
    id "architectury-plugin" version "3.4+"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
    id "me.shedaniel.unified-publishing" version "0.1.+" apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
    repositories {
        maven { url "https://maven.neoforged.net/releases/" }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = minecraft_version + "-" + mod_version
    group = rootProject.maven_group



    repositories {
        maven {
            url "https://jm.gserv.me/repository/maven-public/"
        }
        maven {
            url "https://maven.isxander.dev/releases"
        }
        maven {
            url "https://www.cursemaven.com"
        }
        maven {
            name = 'GitHubPackages'
            url uri("$mod_mvngit_source")
            credentials{
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }

        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
        jcenter()
        maven { url "https://jitpack.io" }
    }
    java {

        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}
subprojects {
    apply plugin: "me.shedaniel.unified-publishing"
    ext {
        releaseChangelog = {
            def dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm")
            dateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))
            def branch
            if (System.env.BRANCH_NAME) {
                branch = System.env.BRANCH_NAME
                branch = branch.substring(branch.lastIndexOf("/") + 1)
            } else {
                branch = "git rev-parse --abbrev-ref HEAD".execute().in.text.trim()
            }
            if (branch == "HEAD") {
                branch = "git rev-parse --short HEAD".execute().in.text.trim()
            }
            def time = dateFormat.format(new Date())
            def changes = new StringBuilder()
            changes << "## " + stage.capitalize() + " $version  \nUpdated at **$time**.  \n**Changelog**  "
            def proc = "git log --max-count=4 --pretty=format:%s".execute()
            proc.in.eachLine { line ->
                def processedLine = line.toString()
                if (!processedLine.contains("New translations") && !processedLine.contains("Merge") && !processedLine.contains("branch")) {
                    def lines = processedLine.split(" -")
                    changes << "\n**${lines[0]}**  "
                    lines[0] = ""
                    lines.each {s-> changes << "\n- " + s + "  "}
                }
            }
            changes << "\n[Click here for complete changelog]($mod_source/commits/$branch)"
            proc.waitFor()
            return changes.toString()
        }
    }
    task renameJarForPublication(type: Zip, dependsOn: remapJar) {
        from remapJar.archiveFile.map { zipTree(it) }
        archiveExtension = "jar"
        metadataCharset "UTF-8"
        destinationDirectory = base.libsDirectory
        archiveClassifier = project.name
    }

    assemble.dependsOn renameJarForPublication
    var modPlatformName = project.name.capitalize()

    processResources {
        def properties =[
                "version" : "${mod_version}",
                "mod_id" : "${mod_id}",
                "mod_name" : "${mod_name}",
                "mod_license" : "${mod_license}",
                "mod_description" : "${mod_description}",
                "mod_authors" : "${mod_authors}",
                "mod_credits" : "${mod_credits}",
                "mod_group" : project.group,
                "mod_source" : "${mod_source}",
                "mod_issues" : "${mod_issues}",
                "mc_version" : "${minecraft_version}"
        ]
        filesMatching(["META-INF/mods.toml","META-INF/neoforge.mods.toml","fabric.mod.json"]) {
            expand properties
        }
        inputs.properties(properties)
    }
    unifiedPublishing {
        project {
            def isFabric = project.name ==  "fabric"
            displayName = "Legacy4J [$modPlatformName] $project.version"
            releaseType = "$stage"
            changelog = releaseChangelog()
            gameVersions = ["$minecraft_version","1.21.1"]
            gameLoaders = ["$project.name"]
            if (isFabric) gameLoaders.addAll "quilt"
            mainPublication renameJarForPublication

            var CURSE_API_KEY = project.findProperty("CURSE_API_KEY") ?: System.getenv("CURSE_API_KEY")
            if (CURSE_API_KEY != null) {
                curseforge {
                    token = CURSE_API_KEY
                    id = rootProject.curseforge_id
                    gameVersions.addAll(["Java 21"])
                    relations{
                        if (isFabric) depends "fabric-api"
                    }
                }
            }


            var MODRINTH_TOKEN = project.findProperty("gpr.token") ?: System.getenv("MODRINTH_TOKEN")
            if (MODRINTH_TOKEN != null) {
                modrinth {
                    token = MODRINTH_TOKEN
                    id = rootProject.modrinth_id
                    version = "$project.version+$project.name"
                    relations{
                        if (isFabric) depends "fabric-api"
                    }
                }
            }
        }
    }
}