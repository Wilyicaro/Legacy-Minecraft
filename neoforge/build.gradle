plugins {
    id "com.github.johnrengelman.shadow"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}


loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
    modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"
    api(include("io.github.libsdl4j:libsdl4j:2.28.4-1.6"))

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property "version", "${mod_version}"
    inputs.property "mod_id", "${mod_id}"
    inputs.property "mod_name", "${mod_name}"
    inputs.property "mod_license", "${mod_license}"
    inputs.property "mod_description", "${mod_description}"
    inputs.property "mod_authors", "${mod_authors}"
    inputs.property "mod_credits", "${mod_credits}"
    inputs.property "mod_group", project.group
    inputs.property "mod_source", "${mod_source}"
    inputs.property "mc_version", "${minecraft_version}"
    inputs.property "architectury_version", "${architectury_version}"
    inputs.property "jei_version", "${jei_version}"

    filesMatching("META-INF/neoforge.mods.toml") {
        expand (
                "version" : "${mod_version}",
                "mod_id" : "${mod_id}",
                "mod_name" : "${mod_name}",
                "mod_license" : "${mod_license}",
                "mod_description" : "${mod_description}",
                "mod_authors" : "${mod_authors}",
                "mod_credits" : "${mod_credits}",
                "mod_group" : project.group,
                "mod_source" : "${mod_source}",
                "mc_version" : "${minecraft_version}",
                "architectury_version" : "${architectury_version}",
                "jei_version" : "${jei_version}"
        )
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "legacy.common.json"
    exclude "legacy.accessWidener"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = "dev-shadow"
}


remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    atAccessWideners.add "legacy.accesswidener"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    afterEvaluate {
        [":common"].forEach {
            def depSources = project(it).sourcesJar
            dependsOn depSources
            from(depSources.archiveFile.map { zipTree(it) }) {
                exclude "legacy.accesswidener"
            }
        }
    }
}


components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenNeoForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
